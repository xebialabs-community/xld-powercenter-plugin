import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
  repositories {
    mavenLocal()
    maven {
      url 'https://oss.sonatype.org/service/local/repositories/releases/content'
    }
    mavenCentral()
  }
  dependencies {
    classpath "com.xebialabs.gradle.plugins:integration-server-gradle-plugin:${integrationServerGradlePluginVersion}"
  }
}

plugins {
  id "com.github.hierynomus.license" version "${licensePluginVersion}"
  id "com.xebialabs.xldp" version "${xldpPluginVersion}"
  id 'nebula.release' version "${nebulaReleasePluginVersion}"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'integration.server'

integrationServer {
  servers {
    controlPlane {
      dockerImage = "xebialabs/xl-deploy"
      version = "10.2.2"
      httpPort = 4516
      overlays = [
        "plugins/xld-official": [
          fileTree(dir: "${buildDir}", includes: ["**/xld-powercenter*.xldp"])
        ]
      ]
      yamlPatches = [
        'centralConfiguration/deploy-server.yaml': [
          'deploy.server.label': 'Deploy with PowerCenter Plugin'
        ]
      ]
    }
  }
}

if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}

if (!project.hasProperty('release.useLastTag')) {
  project.ext['release.useLastTag'] = true
}


processResources.configure {
  filter ReplaceTokens, tokens: [
    'project.version': version.toString(),
    'project.name'   : rootProject.name
  ]
}

license {
  header rootProject.file('License.md')
  strictCheck false
  ext.year = '2020'
  ext.name = 'Digital.ai'
}

rootProject.afterEvaluate {
  startIntegrationServer.dependsOn(build)
}
